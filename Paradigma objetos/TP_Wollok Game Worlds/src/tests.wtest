import Jugadores.*
import Partidas.*
import Equipos.*

// ---------------------------------------------------------
// ðŸ”¸ Mi universo.
// ---------------------------------------------------------
// Jugadores.
const unJugadorNoTitular = new Jugador(antiguedad = 2)

// Partidas.
const unaPartidaIndividual = new Partida(equipoRojo = faker, equipoAzul = caps)
const unaPartidaEnEquipo = new Partida(equipoRojo = unEquipoGanador, equipoAzul = unEquipoPerdedor)
const unaPartidaMixta = new Partida(equipoRojo = faker, equipoAzul = unEquipoNormal)

// Equipo.
const unEquipoNormal = new Equipo(integrantes = [caps, josedeodo, unJugadorNoTitular])
const unEquipoGanador = new Equipo(integrantes = [faker, unJugadorNoTitular])
const unEquipoPerdedor = new Equipo(integrantes = [caps, josedeodo])

// ---------------------------------------------------------
// ðŸ”¸ Jugadores.
// ---------------------------------------------------------
describe "Jugadores profesionales" {
	test "Es posible saber si es titular" {
		// Si tiene mas de 3 anios de antiguedad, entonces es titular.
		assert.that(caps.esTitular())
		
		// Si tiene menos de 3 anios de antiguedad, entonces no es titular.
		assert.notThat(unJugadorNoTitular.esTitular())
	}
	
	test "Es posible saber si esta totalmente cansado" {
		// Si su nivel de cansancio es menor a 100, entonces no esta totalmente cansado.
		assert.notThat(caps.totalmenteCansado())
		
		// Si su nivel de cansancio es 100 o mas, entonces esta totalmente cansado.
		caps.establecerCansancio(100)
		assert.that(caps.totalmenteCansado())
	}
	
	test "Es posible saber si puede jugar" {
		// Si es titular y no esta totalmente cansado, entonces puede jugar.
		assert.that(caps.puedeJugar())
		
		// Si es titular y esta totalmente cansado, entonces no puede jugar.
		caps.establecerCansancio(100)
		assert.notThat(caps.puedeJugar())
		
		// Si no es titular y no esta totalmente cansado, entonces no puede jugar.
		assert.notThat(unJugadorNoTitular.puedeJugar())
		
		// Si no es titular y no esta totalmente cansado, entonces no puede jugar.
		unJugadorNoTitular.establecerCansancio(100)
		assert.notThat(unJugadorNoTitular.puedeJugar())
	}
	
	test "Es posible tomar bebidas energizantes (Baja 10 puntos su nivel de cansancio)" {
		// Si su nivel de cansancio es mayor a 10, entonces es igual a su nivel de cansancio - 10.
		caps.tomarBebidaEnergizante()
		assert.equals(40, caps.cansancio())
		
		// Si su nivel de cansancio es menor a 10, entonces es igual a 0.
		caps.establecerCansancio(5)
		caps.tomarBebidaEnergizante()
		assert.equals(0, caps.cansancio())
	}
	
	test "Es posible jugar (Aumenta el nivel de cansancio en 20 puntos)" {
		caps.jugar()
		assert.equals(70, caps.cansancio())
	}
	
	test "Es posible obtener el nivel de habilidad (Nivel de antiguedad * 2 - cansancio)" {
		assert.equals(-34, caps.habilidad())
	}
}

describe "Faker" {
	test "Es posible saber si esta tilteado" {
		assert.notThat(faker.estaTilteado())
	}
	
	test "Es posible saber si esta totalmente cansado" {
		// Si no esta tilteado, entonces puede jugar.
		assert.that(faker.puedeJugar())
		
		// Si esta tilteado, entonces no puede jugar.
		faker.tiltear()
		assert.notThat(faker.puedeJugar())
	}
	
	test "Es posible tomar un tecito (Deja de estar tilteado)" {
		faker.tiltear()
		faker.tomarTecito()
		assert.notThat(faker.estaTilteado())
	}
	
	test "Es posible jugar (Aumenta la habilidad acumulado en 30 puntos)" {
		faker.jugar()
		assert.equals(30, faker.habilidadAcumulada())
	}
	
	test "Es posible obtener su nivel de habilidad" {
		// Si no esta tilteado, entonces es igual a la habilidad acumulada.
		faker.jugar()
		assert.equals(30, faker.habilidad())
		
		// Si esta tilteado, entonces es igual a 0.
		faker.tiltear()
		assert.equals(0, faker.habilidad())
	}
}

// ---------------------------------------------------------
// ðŸ”¸ Jugadores.
// ---------------------------------------------------------
describe "Equipo" {
	test "Es posible saber los jugadores que estan disponibles (Pueden jugar)" {
		assert.equals([caps, josedeodo], unEquipoNormal.jugadoresDisponibles())
	}
	
	test "Es posible jugar (Solo jugadores disponibles)" {
		unEquipoNormal.jugar()
		assert.equals(70, caps.cansancio())
		assert.equals(70, josedeodo.cansancio())
		assert.equals(50, unJugadorNoTitular.cansancio())
	}
	
	test "Es posible obtener el nivel de habilidad (Igual a la suma de las habilidades de todos los integrantes)" {
		assert.equals(-118, unEquipoNormal.habilidad())
	}
}

// ---------------------------------------------------------
// ðŸ”¸ Partidas.
// ---------------------------------------------------------
describe "Partida individual" {
	test "Es posible comenzar la partida" {
		unaPartidaIndividual.comenzarPartida()
		assert.equals(30, faker.habilidad())
		assert.equals(70, caps.cansancio())
	}
	
	test "Es posible obtener al ganador" {
		unaPartidaIndividual.comenzarPartida()
		assert.equals(faker, unaPartidaIndividual.ganador())
	}
}

describe "Partida equipo" {
	test "Es posible comenzar la partida" {
		unaPartidaEnEquipo.comenzarPartida()
		assert.equals(30, faker.habilidad())
		assert.equals(50, unJugadorNoTitular.cansancio())
		assert.equals(70, caps.cansancio())
		assert.equals(70, josedeodo.cansancio())
	}
	
	test "Es posible obtener al ganador" {
		unaPartidaEnEquipo.comenzarPartida()
		assert.equals(unEquipoGanador, unaPartidaEnEquipo.ganador())
	}
}

describe "Partida mixta" {
	test "Es posible comenzar la partida" {
		unaPartidaMixta.comenzarPartida()
		assert.equals(30, faker.habilidad())
		assert.equals(50, unJugadorNoTitular.cansancio())
		assert.equals(70, caps.cansancio())
		assert.equals(70, josedeodo.cansancio())
	}
	
	test "Es posible obtener al ganador" {
		unaPartidaMixta.comenzarPartida()
		assert.equals(faker, unaPartidaMixta.ganador())
	}
}